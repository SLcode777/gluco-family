/*
 * FreeStyle, Libre, and related brand marks are trademarks of Abbott Diabetes Care Inc. in various jurisdictions.
 Other trademarks are the property of their respective owners.
 * This software is not affiliated with Abbott Diabetes Care, Inc. or any of its subsidiaries

 * Dexcom and related brand marks are trademarks of Dexcom, Inc. in various jurisdictions. Other trademarks are the property of their respective owners.
 * This software is not affiliated with Dexcom, Inc. or any of its subsidiaries
 */

 // ============ Gluco-Monitor versions  ===========

 /*
v1.0 : 1er version de base / First Version Freestyle Only in mg/dL
v2.0 : Ajout du support du Dexcom  / Added Dexcom  support    
v3.0 : Ajout du support des unités mmol/L  / Added support for mmol/L units 
v3.1 : Choix couleurs ou blanc de la valeur de glycémie
       Choix à 10% de luminosité la nuit
v3.2 : Correction du mapping des flèches de tendance Dexcom
       Ajout de la flèche DoubleUp et DoubleDown
       Correction bug luminosité 10% la nuit
*/

//Support available on : https://F1ATB.fr  Documentation and Forum in French and English

#include <WiFi.h>
#include <HTTPClient.h>
#include <ArduinoJson.h>
#include "mbedtls/md.h"
#include <ESPmDNS.h>
#include <ArduinoOTA.h>
#include <esp_task_wdt.h>

#include "Heure.h"
#include "Libreview.h"
#include "Dexcom.h"
#include "Config.h"
#include "Stock.h"
#include "Serie.h"
#include "Internet.h"
#include "Server.h"
#include "Ecran/Gestion.h"
#include "Ecran/pageMessages.h"
#include "Ecran/pageAccueil.h"
#include "Ecran/pageConfiguration.h"
#include "Ecran/pageLibreServeur.h"
#include "Ecran/pageCompte.h"
#include "Ecran/pageInfos.h"
#include "Ecran/pageFuseauH.h"
#include "Langues/Langue.h"

static unsigned long testWatchdog = 0;

#define WDT_TIMEOUT_SECONDS 600 // Watchdog 10 minutes = 600 secondes

void setup()
{
  Serial.begin(115200);
  SetupEnCours=true;
  LaLangue = LANG_NONDEF;
  //=========== Watchdog initialisation ==========
  esp_task_wdt_deinit();
  esp_task_wdt_config_t wdt_cfg = {
      .timeout_ms = WDT_TIMEOUT_SECONDS * 1000UL,
      .idle_core_mask = (1 << portNUM_PROCESSORS) - 1, // Bitmask of all cores
      .trigger_panic = true,
  };
  esp_task_wdt_init(&wdt_cfg);

  // Abonner la tâche Arduino loop() au watchdog
  esp_task_wdt_add(NULL);
  esp_task_wdt_reset();
  delay(1);
  //======= Stockage =============

  LireSerial();
  InitPersons();
  InitStock(); // Init LittleFS
  if (psramInit())
  {
    Serial.println("PSRAM  correctement initialisée");
  }
  else
  {
    Serial.println("La PSRAM ne fonctionne pas");
  }
  LireSerial();

  //========== Anciens paramètres ==============
  ReadFichierParametres();
  LireSerial();
  // =========== Ecran =========================
  bool LangueNonDefini=false;
  if(LaLangue == LANG_NONDEF){
    LaLangue = LANG_FR; //Par defaut
    LangueNonDefini=true;
  }
  InitEcran();
  LireSerial();
  // ===== Definition de la langue si non encore definie ====
  if (LangueNonDefini)
  {
    EcranBienvenue(); // First-boot welcome (shown in French by default, before language is chosen)
    EtapeAssistant(T("Lang"), T("HintLang"), pageLangueSetup);
    EtapeAssistant(T("F_Hor"), T("HintTZ"), pageFuseauSetup);
  }
  // ============ Internet / Wifi et Heure ==============

  Init_Internet();
  CanvaBase->flush();
  esp_task_wdt_reset();
  delay(1);
  Init_Server();
  LireSerial();

  //  ========Modification du programme par le Wifi  - OTA(On The Air) ================

  ArduinoOTA.setHostname((const char *)hostname.c_str());
  ArduinoOTA.begin(); // Mandatory

  LireSerial();

  //======== Demande compte LibreLinkUp ou Dexcom si non défini =====================
  bool compteAssistant = false;
  if (persons[0].sensorType == SENSOR_LIBRE && libreEmail.length() < 4)
  {
    EtapeAssistant(T("LastStep"), T("HintLibre"), CompteSetup);
    compteAssistant = true;
  }
  else if (persons[0].sensorType == SENSOR_DEXCOM && persons[0].dexcomUsername.length() < 4)
  {
    EtapeAssistant(T("LastStep"), T("HintDexcom"), CompteSetup);
    compteAssistant = true;
  }
  if (compteAssistant)
    EcranFinConfig(); // Setup finished: confirm and point to the settings menu

  esp_task_wdt_reset();
  delay(1);
  Serial.printf("PSRAM: %d\n", psramFound());
  Serial.printf("Free heap: %d\n", ESP.getFreeHeap());
  Serial.printf("Free PSRAM: %d\n", ESP.getFreePsram());
  PageActu = pageAccueil; // Always land on the home screen after boot/setup
  SetupEnCours=false;

}

void loop()
{

  LireSerial();
  if (HeureValide)
  {
    // Poll both providers: each one only handles its own persons,
    // so a mixed family (Libre kids + Dexcom parent) works.
    LectureLibre();
    LectureDexcom();
    FormatteHeureDate();
  }
  loopEcran();

//== Update each person's data age (used by display + retry timing) =====
  if (HeureValide)
  {
    time_t now;
    time(&now);
    for (int i = 0; i < MAX_PERSONS; i++)
    {
      if (persons[i].lastGlyUnixTime > 0)
        persons[i].ageSeconds = (long)now - persons[i].lastGlyUnixTime;
    }
  }

  //== Reboot ONLY if the whole acquisition is stuck =====================
  // A single sensor in warmup / being changed must not trigger a reboot.
  // We reboot only when every configured person has had no successful
  // reading for 20 min — that points to a real network/system problem.
  if (allConfiguredPersonsSilent(1210000) && millis() > 300000)
    AlertePasdeGlycemie();
  if (millis() - testWatchdog > 10000)
  {
    testWatchdog = millis();
    if (WiFi.status() == WL_CONNECTED)
    {
      esp_task_wdt_reset(); // Reset du watchdog
      delay(1);
    }
  }

  //======= Page HTML Brute ============

  if (millis() - TimerAutorisationBruteMillis > 180000)
    AutorisationPageBrute = false; // Autorisation pour 3mn

  delay(2);
}
